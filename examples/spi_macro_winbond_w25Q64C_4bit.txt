    # Send 2 dummy bytes.
    DUMMY(2)

    CS(true)
    SEND(0xff, 0xff)
    CS(false)
    DUMMY(1)

    # Send the command 0x9f.
    CS(true)
    SEND(0x9f)
    # Receive 3 bytes.
    CS(true)
    RECEIVE(3)
    CS(false)
    DUMMY(1)
    # Compare the JEDEC ID.
    CMP(0xef, 0x40, 0x17)
    # Fail if it does not match.
    FAIL(NotEqual,'Fail to match JEDEC ID!')

    # Read status register 2.
    CS(true)
    SEND(0x35)
    CS(true)
    RECEIVE(1)
    CS(false)
    DUMMY(1)
    # Get the QE bit.
    MASK(0x02)
    # QE must be 1 to be able to use 4 bit transfer.
    JUMP(NotZero, 'QeOk')
    # Write enable.
    CS(true)
    SEND(0x06)
    CS(false)
    DUMMY(1)
    # Write the status register 1 and 2.
    CS(true)
    SEND(0x01, 0x02, 0x02)
    CS(false)
    DUMMY(1)
    # Poll for write finished.
Poll:
    CS(true)
    SEND(0x05)
    CS(true)
    RECEIVE(1)
    CS(false)
    DUMMY(1)
    # Get the BUSY bit.
    MASK(0x01)
    JUMP(NotZero, 'Poll')

QeOk:
    # Read the SFDP magic.
    CS(true)
    SEND(0x5a, 0x00, 0x00, 0x00, 0x00)
    CS(true)
    RECEIVE(4)
    CS(false)
    DUMMY(1)
    CMP(0x53, 0x46, 0x44, 0x50)
    # Fail if it does not match.
    FAIL(NotEqual,'Fail to match SFDP magic!')
    
    # Read the manufacturer and device ID in 4 bit mode.
    CS(true)
    SEND(0x94)
    # Switch to 4 bit mode.
    MODE(4BIT)
    # Send the address and the mode byte.
    CS(true)
    SEND(0x00, 0x00, 0x00, 0x00)
    # Send 4 idle cycles.
    CS(true)
    IDLE(4)
    # Receive the data.
    CS(true)
    RECEIVE(2)
    CS(false)
    DUMMY(1)    
    # Switch to 1 bit mode.
    MODE(1BIT)
    # Compare the values.
    CMP(0xef, 0x16)
    # Fail if it does not match.
    FAIL(NotEqual,'Fail to match manufacturer and device ID in 4 bit mode!')  
